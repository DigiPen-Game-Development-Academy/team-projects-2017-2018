class DamageOnCollide : ZilchComponent
{
    [Property] var Damage : Integer = 0;
    [Property] var DamageCooldown : Real = 0.5;
    [Property] var PlayerDamagedSound : SoundCue;
    
    var TimeTillDamage : Real = 0;
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.Owner, Events.CollisionPersisted, this.OnCollisionPersisted);
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
      if (this.TimeTillDamage > 0)
      {
        this.TimeTillDamage -= event.Dt;
      }
    }

    function OnCollisionPersisted(event : CollisionEvent)
    {
        if(event.OtherObject.Name == "Player")
        {
          if (this.TimeTillDamage <= 0)
          {
            this.TimeTillDamage = this.DamageCooldown;
            
            this.GameSession.GameVars.PlayerHealth -= this.Damage;
            this.Owner.Transform.Translation += Math.Normalize(this.Owner.Transform.Translation - event.OtherObject.Transform.Translation) * 0.1;
            event.OtherObject.FlashColor.Flash = true;
            this.Space.SoundSpace.PlayCue(this.PlayerDamagedSound);
            
            var playerHurtUpdate = PlayerHurtUpdate();
            //Console.WriteLine("test2");
            this.GameSession.DispatchEvent(Events.PlayerHurtUpdate, playerHurtUpdate);
          }
        }
    }
}